plugins {
	id 'application'
	id 'java'
	id 'org.jetbrains.kotlin.jvm' version '1.5.20'
}

group 'com.tb24'
version '0.4.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
	javaMainClass = 'com.tb24.blenderumap.Main'
}

application {
	mainClassName = javaMainClass
}

jar {
	manifest {
		attributes(
				'Main-Class': javaMainClass
		)
	}

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	} {
		exclude 'META-INF/*.SF'
		exclude 'META-INF/*.DSA'
		exclude 'META-INF/*.RSA'
	}
}

repositories {
	google()
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation 'androidx.collection:collection-ktx:1.1.0'
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
	implementation 'com.google.code.gson:gson:2.8.7'
	implementation 'com.squareup.okhttp3:okhttp:3.14.9'
	implementation 'me.fungames:JFortniteParse:+'
	implementation 'org.slf4j:slf4j-api:1.7.32'
	testImplementation 'junit:junit:4.13.2'
}

compileKotlin {
	kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
	kotlinOptions.jvmTarget = '1.8'
}
